prototype(Dotpulse.ResponsiveTabs:Container) < prototype(Content) {
	attributes {
		class = Neos.Fusion:RawArray {
			container = 'responsive-tabs'
			backend = 'responsive-tabs__neos-backend'
			backend.@if.isInBackend = ${node.context.inBackend}
		}
		id = ${'tablist_' + node.identifier}
	}

	menuAttributes = Neos.Fusion:Attributes {
		class = 'responsive-tabs__tablist'
		role = 'tablist'
	}

	tabMenu = Neos.Fusion:Tag {
		tagName = 'ul'
		attributes {
			class = 'responsive-tabs__tablist'
			role = 'tablist'
		}
		content = Neos.Fusion:Collection {
			collection = ${q(node).children('main').children('[instanceof Dotpulse.ResponsiveTabs:Content]').get()}
			itemName = 'node'
			iterationName = 'iterator'
			itemRenderer = Neos.Fusion:Tag {
				tagName = 'li'
				attributes {
					class = Neos.Fusion:RawArray {
						tab = 'responsive-tabs__tab'
						active = 'responsive-tabs__tab-active'
						active.@if.isFirstElement = ${iterator.index == 0 ? true : false}
					}
					id = ${'tablist_' + node.identifier + '-tab_' + iterator.index}
					aria-controls = ${'tablist_' + node.identifier + '-item_' + iterator.index}
					title = ${q(node).property('title')}
				}
				content = ${q(node).property('title')}
			}
		}
		@if.isNotInBackend = ${!node.context.inBackend}
	}

	content = ContentCollection {
		nodePath = 'main'
		attributes.class = 'responsive-tabs__wrapper'
	}
}

prototype(Dotpulse.ResponsiveTabs:Content) < prototype(Content) {
	attributes {
		id = ${'tablist_' + node.identifier + '-item_' + iterator.index}
		class = Neos.Fusion:RawArray {
			item = 'responsive-tabs__item'
			active = 'responsive-tabs__item-active'
			active.@if.isFirstElement = ${iterator.index == 0 ? true : false}
			@process.nodeType >
		}
	}

	titleAttributes = Neos.Fusion:Attributes {
		class = 'responsive-tabs__title'
	}
	contentAttributes = Neos.Fusion:Attributes {
		class = 'responsive-tabs__content'
	}

	content = ContentCollection {
		nodePath = 'main'
		attributes {
			class = 'responsive-tabs__content-inner'
			class.@process.collectionClass >
		}
	}
}

prototype(Page) {
	head.ResponsiveTabs = Neos.Fusion:Tag {
		tagName = 'link'
		attributes {
			rel = 'stylesheet'
			href = Neos.Fusion:ResourceUri {
				path = 'resource://Dotpulse.ResponsiveTabs/Public/responsive-tabs.min.css'
			}
		}
		@if.isEnabled = ${Configuration.setting('Dotpulse.ResponsiveTabs.includeCss')}
	}
	body.javascripts.ResponsiveTabs = Neos.Fusion:Tag {
		tagName = 'script'
		attributes.src = Neos.Fusion:ResourceUri {
			path = 'resource://Dotpulse.ResponsiveTabs/Public/responsive-tabs.min.js'
		}
		@position = 'end'
		@if.isEnabled = ${Configuration.setting('Dotpulse.ResponsiveTabs.includeJavascript')}
	}
}
